plugins {
    id "org.asciidoctor.jvm.convert" version "3.3.2"  // (1)
//    (1) asciidoctor에 대한 플러그인을 추가해준다 .
//    이 플러그인은 adoc 파일을 변환하고 build 디렉토리에 복사하기 위해 사용하는 플러그인이다. gradle 7 부터는 이전에 사용하던 org.asciidoctor.convert 대신asciidoctor.jvm.convert를 사용해야 한다.
}

configurations {
    asciidoctorExt // (2)
//    (2) asciidoctorExt을 Configuration에 지정해준다.
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor' // (3)
//    (3) dependencies에 spring-restdocs-asciidoctor를 추가해준다.
//    adoc 파일에서 사용할 snippets 속성이 자동으로 build/generated-snippets를 가리키도록 해준다.
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc' // (4)
//    (4) MockMvc를 사용하여 테스트할 예정이기 때문에 spring-restdocs-mockmvc 도 dependencies에 추가해준다.
}

ext {
    snippetsDir = file('build/generated-snippets') // (5)
//    (5) snippets 파일이 저장될 경로를 설정해준다.
}

test {
    useJUnitPlatform()
    outputs.dir snippetsDir // (6)
//    (6) 출력할 디렉토리는 설정해준다.
}

asciidoctor {
    configurations 'asciidoctorExt' // (7)
//    (7) asciidoctor에서 asciidoctorExt을 configurations로 사용하도록 설정한다.
    baseDirFollowsSourceFile() // (8)
//    (8) .adoc 파일에서는 다른 .adoc 파일을 include하여 사용할 수 있는데 그럴 경우 경로를 동일하게 baseDir로 설정해준다. Gradle 6 버전에서는 자동으로 해주지만 7부터는 직접 명시해줘야 한다.
    inputs.dir snippetsDir // (9)
//    (9) input 디렉토리를 설정해준다.
    dependsOn test // (10)
//    (10) build시 test 후 asciidoctor를 진행하도록 설정해준다 . (순서 설정) // 여담) 흐름상 test 가 먼저이니 위로 보내는게 직관적
}

asciidoctor.doFirst {
    delete file('src/main/resources/static/docs')  // (11)
//    (11) 중복을 막기 위해 새로운 문서를 생성할 때에는 전에 생성했던 문서들을 먼저 지워준다 .
}

task copyDocument(type: Copy) { // (12)
//    (12) build / docs / asciidoc 디렉토리에 생성된 html 문서를 src / main / resources / static / docs 디렉토리에 복사해온다 .
//    (13) copyDocument 후 build 지정
    dependsOn asciidoctor
    from file("build/docs/asciidoc")
    into file("src/main/resources/static/docs")
}

build {
    dependsOn copyDocument
}